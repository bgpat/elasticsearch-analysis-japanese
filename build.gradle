buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath 'gradle.plugin.com.github.breadmoirai:github-release:1.0.9'
	}
}

apply plugin: 'java'
apply plugin: 'com.github.breadmoirai.github-release'

defaultTasks 'clean', 'build'

repositories {
	mavenCentral()
}

configurations {
	releaseJars {
		extendsFrom runtime
		exclude group: "org.elasticsearch"
	}
}

dependencies {
	compile files('kuromoji/kuromoji-core/target/kuromoji-core-1.0-SNAPSHOT.jar', 'kuromoji/kuromoji-ipadic-neologd/target/kuromoji-ipadic-neologd-1.0-SNAPSHOT.jar')
	compile 'commons-io:commons-io:2.5'
	compile "org.elasticsearch:elasticsearch:${elasticsearch_version}"
}

task distZip(type: Zip, dependsOn: jar) {
	archiveName = "elasticsearch-analysis-japanese-${project.version}-es${elasticsearch_version}.zip"
	into ('elasticsearch') {
		from jar.archivePath
		from configurations.releaseJars
		from 'build/resources/main/plugin-descriptor.properties'
	}
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
			'elasticsearch_version': project.property('elasticsearch_version')
		])
	}
}

artifacts {
    archives distZip
}

jar {
	into('META-INF') {
		from 'LICENSE'
	}
}

githubRelease {
	token = "${github_token}"
	owner = 'bgpat'
	repo = 'elasticsearch-analysis-japanese'
	releaseAssets = distZip.destinationDir.listFiles()
}
